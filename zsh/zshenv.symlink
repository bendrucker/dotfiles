#!/usr/bin/env zsh

export DOTFILES_HOME="$HOME/.dotfiles"

# Select dotfiles source:
#   DOTFILES_USE_DEV=/path - use specified dev directory (for dotfiles test)
#   ~/.dotfiles-dev-mode   - persistent dev mode (file contains path)
#   default                - use installed directory
if [[ -n "$DOTFILES_USE_DEV" ]]; then
  export ZSH="$DOTFILES_USE_DEV"
elif [[ -f "$HOME/.dotfiles-dev-mode" ]]; then
  export ZSH="$(<"$HOME/.dotfiles-dev-mode")"
else
  export ZSH="$DOTFILES_HOME"
fi

# Fallback if selected directory doesn't exist
[[ ! -d "$ZSH" ]] && export ZSH="$DOTFILES_HOME"

export PROJECTS="$HOME/src"

eval "$(/opt/homebrew/bin/brew shellenv)"

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# After all path files are loaded (including mise), ensure our bins take priority and deduplicate
typeset -gU path
path=("$ZSH/bin" "$HOME/.local/bin" $path)

[[ -f ~/.zshenv.local ]] && source ~/.zshenv.local
