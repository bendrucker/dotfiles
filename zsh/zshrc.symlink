#!/usr/bin/env zsh

if [[ -n "$ZPROF" ]]; then
  zmodload zsh/zprof
fi

export PATH="/usr/local/bin:/usr/local/sbin:$ZSH/bin:$PATH"

export ZSH="$HOME/.dotfiles"
export PROJECTS="$HOME/src"

if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# https://docs.brew.sh/Shell-Completion#configuring-completions-in-zsh
if type brew &>/dev/null
then
  FPATH="$(brew --prefix)/share/zsh/site-functions:${FPATH}"

  autoload -Uz compinit
  compinit
fi

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

if [[ -n "$ZPROF" ]]; then
  zprof
fi
