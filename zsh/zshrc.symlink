#!/usr/bin/env zsh

if [[ -n "$ZPROF" ]]; then
  zmodload zsh/zprof
fi

export PATH="$ZSH/bin:$HOME/.local/bin:$PATH"

for localrc in ~/.localrc ~/.zshrc.local; do
  [[ -f $localrc ]] && source $localrc
done

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

if type brew &>/dev/null
then
  FPATH="$(brew --prefix)/share/zsh/site-functions:${FPATH}"
fi

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# Ensure user paths take priority after mise activation and deduplicate
typeset -gU path
path=("$ZSH/bin" "$HOME/.local/bin" $path)

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete
autoload -Uz compinit
compinit

# load every completion
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

if [[ -n "$ZPROF" ]]; then
  zprof
fi
